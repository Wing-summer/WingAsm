cmake_minimum_required(VERSION 3.16)

project(WingHexAsm LANGUAGES CXX)

set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)

set(CMAKE_CXX_STANDARD 14)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_INCLUDE_CURRENT_DIR TRUE)

set(QT_CREATOR_SKIP_MAINTENANCE_TOOL_PROVIDER ON)

if(POLICY CMP0148)
    set(CMAKE_POLICY_DEFAULT_CMP0148 OLD)
endif()

if(MSVC)
    string(APPEND CMAKE_CXX_FLAGS " /utf-8")
    string(APPEND CMAKE_C_FLAGS " /utf-8")
endif()

option(TEST_MODE OFF)

add_subdirectory(WingPlugin)

find_package(Qt6 REQUIRED COMPONENTS Widgets LinguistTools)

set(TS_FILES WingHexAsm_zh_CN.ts WingHexAsm_zh_TW.ts WingHexAsm_en_US.ts)

set(TRANSLATION_PATH "${CMAKE_CURRENT_SOURCE_DIR}/src")

set(CMAKE_POSITION_INDEPENDENT_CODE ON)

if(POLICY CMP0063)
    set(CMAKE_POLICY_DEFAULT_CMP0063 NEW)
endif()

add_subdirectory(3rdparty/QCodeEditor)
add_subdirectory(3rdparty/capstone)
add_subdirectory(3rdparty/keystone)

# disable the fucking warnnings generated by 3rdparty
if(MSVC)
    target_compile_options(capstone PRIVATE /w)

    file(GLOB files "3rdparty/keystone/suite/fuzz/fuzz*.c")
    foreach(file ${files})
        get_filename_component(FUZZ_TARGET ${file} NAME_WE)
        target_compile_options(${FUZZ_TARGET} PRIVATE /w)
    endforeach()
    target_compile_options(keystone PRIVATE /w)
else()
    target_compile_options(capstone PRIVATE -w)
    target_compile_options(keystone PRIVATE -w)
endif()

qt6_create_translation(QM_FILES ${TRANSLATION_PATH} ${TS_FILES} OPTIONS
                       -no-obsolete)

include(${CMAKE_CURRENT_SOURCE_DIR}/cmake/TranslationUtils.cmake)
add_plugin_translations_resource(QM_RES WingHexAsm ${QM_FILES})

add_library(
    WingHexAsm SHARED
    ${QM_FILES}
    ${QM_RES}
    src/winghexasm.cpp
    src/winghexasm.h
    src/WingHexAsm.json
    resources.qrc
    src/wingengine.h
    src/wingengine.cpp
    src/asmwindow.h
    src/asmwindow.cpp
    src/asmwindow.ui
    src/syntaxconfigparser.h
    src/syntaxconfigparser.cpp
    src/asmhighlighter.h
    src/asmhighlighter.cpp)

set_target_properties(WingHexAsm PROPERTIES SUFFIX ".wingplg")

if(TEST_MODE)
    if(NOT EXISTS "${WINGHEX_PATH}")
        message(
            FATAL_ERROR "Please config the WingHexExplorer2 Path - WINGHEX_PATH"
        )
    endif()

    set(WINGHEX_PLUGIN_PATH "${WINGHEX_PATH}/plugin")
    add_custom_command(
        TARGET WingHexAsm
        POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E make_directory ${WINGHEX_PLUGIN_PATH}
        COMMAND ${CMAKE_COMMAND} -E copy_if_different $<TARGET_FILE:WingHexAsm>
                ${WINGHEX_PLUGIN_PATH})
endif()

target_link_libraries(WingHexAsm PRIVATE Qt6::Widgets WingPlugin QCodeEditor
                                         capstone keystone)
